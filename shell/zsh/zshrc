# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
F="$XDG_CACHE_HOME/p10k-instant-prompt-${(%):-%n}.zsh" && [[ -f $F ]] && source $F


###############
# Zsh options
###############

SAVEHIST=50000
HISTFILE="$XDG_DATA_HOME/zsh/history"
HISTSIZE=50000
HISTORY_IGNORE='(l|ls|cd|exit|q|nvim|htop)'
setopt HIST_IGNORE_DUPS
setopt HIST_IGNORE_SPACE
setopt INC_APPEND_HISTORY
setopt EXTENDED_HISTORY

setopt INTERACTIVE_COMMENTS
setopt AUTO_CD


###############
# Interactive options
###############

export LS_COLORS=$LS_COLORS:"di=1;33:ex=1;31:ln=4;36:*.mkv=35"
export CHEATCOLORS=true
export BAT_THEME="gruvbox-dark"
export FZF_DEFAULT_COMMAND="rg --files --hidden --glob '!\.git'"
export FZF_DEFAULT_OPTS="--bind=alt-j:preview-down,alt-k:preview-up,\
ctrl-alt-d:preview-half-page-down,ctrl-alt-u:preview-half-page-up"
export MCFLY_KEY_SCHEME=vim
export MCFLY_FUZZY=true


###############
# Functions and aliases
###############

function man {
    # Enable colors for manpages
    LESS_TERMCAP_md=$'\e[01;31m' \
    LESS_TERMCAP_me=$'\e[0m' \
    LESS_TERMCAP_se=$'\e[0m' \
    LESS_TERMCAP_so=$'\e[0;44;39m' \
    LESS_TERMCAP_ue=$'\e[0m' \
    LESS_TERMCAP_us=$'\e[01;32m' \
    command man "$@"
}

function md {
    # Faster creation of directories
    mkdir --parents $1 && cd $1
}

function cpprun {
    local executable=${1%.cpp}
    g++ -std=c++20 -Wall -Wextra -g $1 -o $executable && ./$executable
}

function dirsize {
    # View disk usage of a directory
    du --all --total --human-readable --max-depth=1 $1 | sort -hr | less
}

function __is_binary {
    file --mime-encoding $1 |& grep -q 'binary$'
}

alias l="exa -l"
alias o="handlr open"
function v {
    # List directory, preview text file, or open binary file
    if [[ -d $1 ]]; then
        l $1
    elif [[ -f $1 ]]; then
        if __is_binary $1; then
            o $1
        else
            bat $1
        fi
    else
        printf "Could not handle %s" $(file $1)
    fi
}
alias q="exit"
alias zreload="source $HOME/.zshrc"
alias sudo="sudo "

alias ls="ls --color=auto"
alias sysu="systemctl --user"
alias adb="$XDG_DATA_HOME/android/sdk/platform-tools/adb"

alias fvim='nvim $(fd --hidden | fzf)'
alias fcd='cd $(fd --hidden --type d | fzf)'
function fzo {
    # Enter to open file
    # CTRL-F to open its directory
    # CTRL-O to cd to its directory
    local fzf_header="CTRL-F: open dir    CTRL-O: cd dir

"
    local result=$(fd --follow --type f . $@ | fzf --reverse --expect=ctrl-o,ctrl-f --header $fzf_header)
    local file=$(tail -n1 <<< $result)
    local cmd=$(head -n1 <<< $result)
    case $cmd in
        ctrl-o)
            zsh -c "cd $(dirname $file); zsh -i"
            ;;
        ctrl-f)
            (o $(dirname $file) & disown) > /dev/null 2>&1
            ;;
        *)
            (o $file & disown) > /dev/null 2>&1
            ;;
    esac
}

function cppass {
    keepassxc-cli clip $HOME/doc/phone/passwords.kdbx $1 10
}
alias labs='curl --silent http://liis.ro/hosted/activitati/2013/laboratoare/labs.pdf | zathura -'


###############
# Bindings
###############

# Vi mode
bindkey -v
export KEYTIMEOUT=1

autoload history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end

bindkey "^P"   history-beginning-search-backward-end
bindkey "^N"   history-beginning-search-forward-end
bindkey "^[[A" up-line-or-history
bindkey "^[[B" down-line-or-history

bindkey "^?"   backward-delete-char
bindkey "^H"   backward-kill-word

autoload edit-command-line
zle -N edit-command-line
bindkey "^E" edit-command-line

# Help
unalias run-help
autoload run-help

# Completion
fpath=($ZDOTDIR/completions $fpath)
autoload -Uz compinit
compinit

# Fuzzy completion from https://superuser.com/a/815317
zstyle ':completion:*' matcher-list '' \
    'm:{a-z\-}={A-Z\_}' \
    'r:[^[:alpha:]]||[[:alpha:]]=** r:|=* m:{a-z\-}={A-Z\_}' \
    'r:|?=** m:{a-z\-}={A-Z\_}'

# Prompt
export PS1="%F{yellow} %~ î‚± %f"
export RPS1="%(?..%F{red}[%?])"
F=$XDG_CONFIG_HOME/zsh/p10k.zsh && [[ -f $F ]] && source $F


###############
# Plugins
###############

(( $+commands[pyenv] )) && eval "$(pyenv init -)"
(( $+commands[zoxide] )) && eval "$(zoxide init zsh)"
(( $+commands[mcfly] )) && eval "$(mcfly init zsh)"

declare -A ZINIT
ZINIT[HOME_DIR]=$XDG_DATA_HOME/zinit
source $ZINIT[HOME_DIR]/bin/zinit.zsh

zinit ice depth=1
zinit light romkatv/powerlevel10k

zinit load wfxr/forgit

zinit ice as=program pick="bin/git-fuzzy"
zinit light bigH/git-fuzzy
